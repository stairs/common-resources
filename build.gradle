buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}


apply plugin: 'com.android.library'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            workingDir './ui-native-common'
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-0.${getVersionCode}.aar"
            }
        }
    }
}

clean {
    delete fileTree('src/main/res/')
}

task i18n(type: Exec) {
    commandLine "/usr/local/bin/twine", "generate-all-localization-files", "ui-native-common/strings/twine.txt", "src/main/res", "--format", "android", "--tags", "common,android"
}

task configs(type: Copy) {
    from "ui-native-common/config"
    into "src/main/res/raw"
}

task gulp(type: Exec) {
    workingDir "./ui-native-common"
    commandLine "/usr/local/bin/gulp", "images", "--platform=android"
}

task images(dependsOn: gulp, type: Copy) {
    doFirst {
        delete "${rootDir}/ui-native-common/assets/tmp/android/res/drawable/iOS"
    }
    from "ui-native-common/assets/tmp/android/res"
    into "src/main/res"
    doLast {
        delete "${rootDir}/ui-native-common/assets/tmp"
    }
}

preBuild.dependsOn i18n
preBuild.dependsOn configs
preBuild.dependsOn images
